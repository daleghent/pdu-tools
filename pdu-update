#!/opt/local/bin/perl

use strict;
use warnings;

use Carp;
use URI;
use Getopt::Std;
use Net::OpenSSH;
use Net::Telnet;
use Net::FTP;
use File::Temp;
use Data::Validate::IP;
use Socket;

use vars qw/ %opt /;

my $opts = 'chu:U:P:';
getopts("$opts", \%opt) or usage();
usage() if $opt{h};

#
# ServerTech CDU/PDU default creds are 'admn' and 'admn'
# We first try to read the credentials from two env vars:
# $PDU_USER and $PDU_PASS
#
# If the -U or -P flags were specified, we then use the
# values provided with them.
#
my $pdu_user = $ENV{'PDU_USER'} || 'admn';
my $pdu_pass = $ENV{'PDU_PASS'} || 'admn';
$pdu_user = $opt{U} if $opt{U};
$pdu_pass = $opt{P} if $opt{P};

#
# The FTP server paramters we program into each PDU
# Define a default, and then use -u if specified.
#
# NOTE: as of firmware 7.1c and 8.0k, only FTP is supported
# on ServerTech PDUs as a firmware fetching protocol.
#
my $url = 'ftp://ftp:pdu-update@10.66.128.19/pdu/sentry';
$url = $opt{u} if $opt{u};

my $fw_url = URI->new($url);

if ($fw_url->scheme() ne "ftp") {
	die "Currently only ftp:// URLs are supported!";
}

my $ftp_userinfo = $fw_url->userinfo();

my $ftp_user = $1 if $ftp_userinfo =~ /(.*):.*/;
my $ftp_pass = $1 if $ftp_userinfo =~ /.*:(.*)/;
my $ftp_server = $fw_url->host();
my $ftp_path = $fw_url->path();

my @pdus;
if (@ARGV) {
	@pdus = @ARGV;
} else {
	usage();
}

my $ip_validator = Data::Validate::IP->new;

#
# Iterate over each IP address given on the command line
#
foreach my $pdu_ip (@pdus) {

	# Resolve hostnames to IPs, and mke sure we get a valid IP.
	unless ( $ip_validator->is_ipv4($pdu_ip) ) {
		my $packed_ip = inet_aton($pdu_ip) or croak "could not resolve $pdu_ip";
		$pdu_ip = inet_ntoa($packed_ip);
	}

	print "Connecting to " . $pdu_ip . "...\n";

	# In order to talk to ServerTech PDUs, we need to enable some crypto
	# options which OpenSSH has removed from its default set (read: they're
	# bad crypto) so we initiate the OpenSSH object with options to enable
	# them, as well as quiet the Host Key checking. We then initialize a
	# Net::Telnet object and wire that over the OpenSSH's pty to issue
	# commands to the PDU's CLI.
	my $ssh = Net::OpenSSH->new($pdu_ip, (
		master_opts => [
			-o => "KexAlgorithms=+diffie-hellman-group1-sha1",
			-o => "HostKeyAlgorithms=+ssh-dss",
			-o => "Ciphers=+aes256-cbc",
			-o => "LogLevel=QUIET",
			-o => "StrictHostKeyChecking=no"
			],
		user => $pdu_user,
		password => $pdu_pass
	));

	my ($pty, $pid) = $ssh->open2pty({stderr_to_stdout => 1})
		or die "unable to start remote shell: " . $ssh->error;

	my $telnet = Net::Telnet->new(
		Fhopen => $pty,
		# Dump_Log => '/tmp/dumplog',
		Telnetmode => 0,
		Cmd_remove_mode => 1,
		Timeout => 10,
		Errmode => 'die',
		Prompt  => '/Switched [CP]DU: /'
	);

	my %pdu_info;
	my @output;

	$telnet->waitfor($telnet->prompt);
	@output = $telnet->cmd('set option more disabled')
		or die "command failed: set option more disabled";

	my $prompt = $telnet->last_prompt;

	if ($prompt =~ /Switched PDU:/) {
		$pdu_info{type} = "pro";
	} elsif ($prompt =~ /Switched CDU:/)  {
		$pdu_info{type} = "cdu";
	} else {
		die "FATAL: Could not determine PDU type!";
	}

	@output = $telnet->cmd('show system')
		or die "command failed: show system";

	foreach (@output) {
		if (/^\s+F\/W Version:\s+Sentry Switched CDU Version (.*)/ ||
		    /^\s+Firmware:\s+Sentry Switched PDU Version (.*)/) {
			$pdu_info{fw_ver} = $1;
		}
	}

	print "$pdu_ip is of type " . $pdu_info{type} . "\n";

	# Connect to the firmware FTP server and GET either of
	# current-swcdu.txt or current-pro.txt, and look at their
	# contents to know what the current desired firmware version is
	# for PRO or non-PRO PDUs. We use this info to see if we need to upgrade
	# and, if so, to retrieve the desired firmware image.
	my $ftp = Net::FTP->new($ftp_server)
		or die "Cannot connect: $@";

	$ftp->login($ftp_user, $ftp_pass)
		or die "FTP login failed: $@";

	$ftp->passive(1)
		or die "FTP set passive failed: $@";

	$ftp->cwd($ftp_path)
		or die "FTP chdir failed: $@";

	my $tmp = File::Temp->new(
		TEMPLATE => 'tmpXXXXX',
		DIR => '/tmp');

	my $ver_file;
	my $cmd_file;
	if ($pdu_info{type} eq "cdu") {
		$ver_file = 'current-swcdu.txt';
		$cmd_file = 'cmds-swcdu.txt';
	} elsif ($pdu_info{type} eq "pro") {
		$ver_file = 'current-pro.txt';
		$cmd_file = 'cmds-pro.txt';
	}

	# Save the retrieved version file to a File::Tmp file.
	$ftp->get($ver_file, $tmp->filename)
		or die "FTP could not fetch $ver_file: $@";

	$ftp->quit;

	# Read the tmp file contents, store the version as a variable.
	my $current_ver = slurp($tmp->filename);
	chomp($current_ver);

	$tmp->DESTROY;

	# Run any commands from command files if -c was specified
	if ($opt{c}) {
		print "  Running commands in $cmd_file...\n";
		my $cmds = slurp($cmd_file);
		$telnet->print($cmds);
	}

	# See if we need to upgrade the PDU. Do so if yes.
	if ($pdu_info{fw_ver} ne $current_ver) {
		print "PDU FW out of date!\n" .
			"  Has $pdu_info{fw_ver}, " .
			"Want $current_ver\n"; 

		update_fw(\%pdu_info, $current_ver, $telnet);
	} else {
		print "$pdu_ip is up to date: $pdu_info{fw_ver}\n";
	}

	# Close down and go to the next PDU - or quite if no more.
	$telnet->close;
	waitpid($pid, 0);
}

#
# Helper functions
#

#
# Sets up PDU with firmware FTP settings and initiates PDU reboot in order
# to download and apply the new firmware.
#
sub update_fw {
	my $pinfo = shift;
	my $fw_ver = shift;
	my $telnet = shift;

	my %pdu_info = %$pinfo;

	# Derive the firmware filename we want based on whether we are
	# a PRO or non-PRO PDU, and deired firmware version string (with
	# no dots).
	$fw_ver =~ s/\.//;

	my $fw_file;
	if ($pdu_info{type} eq "cdu") {
		$fw_file = "swcdu-v" . $fw_ver . ".bin";
	} elsif ($pdu_info{type} eq "pro") {
		$fw_file = "pro-v" . $fw_ver . ".bin";
	}

	# Set up our ftp parameters in order to fetch the firmware
	print "  Setting FTP parameters in PDU...\n";

	$telnet->print('set ftp host ' . $ftp_server) 
		or die "cmd set ftp host failed";
	$telnet->print('set ftp username ' . $ftp_user) 
		or die "cmd set ftp username failed";
	$telnet->print('set ftp password ' . $ftp_pass) 
		or die "cmd set ftp password failed";
	$telnet->print('set ftp directory ' . $ftp_path) 
		or die "cmd set ftp directory failed";
	$telnet->print('set ftp filename ' . $fw_file) 
		or die "cmd set ftp filename failed";


	# Initiate firmware update with 'restart ftpload'
	print "  Rebooting PDU to load new firmware...\n";
	$telnet->print('restart ftpload')
		or die "cmd restart ftpload failed";

	# Answer confirm reboot yes/no prompt
	# We need to pause for 2 seconds after answering because if we
	# close the session too quickly, the reboot process will abort.
	$telnet->waitfor('/want to restart\?? \(Y\/es N\/o\): /')
		or die "did not receive restart confirm prompt: " .
			$telnet->lastline;
	$telnet->print('y');
	sleep 2;
}

#
# Open a file an slurp its contents into a variable
#
sub slurp {
	my $file = shift;

	open my $fh, '<', $file
		or die "Could not open $file: $@";
	local $/ = undef;
	my $content = <$fh>;
	close $fh;

	return $content;
}

#
# Usage info
#
sub usage {
	print STDERR << "EOF";
Usage: $0 [-h] [-u url] [-U user] [-P password] pdu_ip ...

	-h	This help output
	-c	Program the PDU with the contents of cmds-pro.txt or
		  cmds-swcdu.txt (done regardless of firmware status)
	-u	PDU firmware in URL format (ftp://user:pass\@host/path/)
	-U	PDU user (or \$PDU_USER env var) Default: admn
	-P	PDU password (or \$PDU_PASS env var) Default: admn
EOF
	exit;
}
